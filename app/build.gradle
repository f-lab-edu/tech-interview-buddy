plugins {
	id 'org.springframework.boot' version '3.5.3'
}

dependencies {
	// Common 모듈 의존성
	implementation project(':common')
	
	// Spring Boot Web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	
	runtimeOnly 'com.mysql:mysql-connector-j'
	
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api:2.1.1'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'
	
	implementation 'org.springframework:spring-context'
	implementation 'org.springframework:spring-tx'
	
	implementation 'com.github.ben-manes.caffeine:caffeine'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'com.h2database:h2'
	
	testImplementation 'org.apache.groovy:groovy:4.0.15'
	testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
	testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
}

springBoot {
	mainClass = 'com.example.tech_interview_buddy.app.TechInterviewBuddyApplication'
}

jar {
	enabled = false
}

bootJar {
	enabled = true
	archiveBaseName = 'tech-interview-buddy-app'
	archiveVersion = '0.0.1-SNAPSHOT'
}

def querydslDir = "$buildDir/generated/querydsl"

sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

tasks.withType(JavaCompile) {
	options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
}

clean.doLast {
	file(querydslDir).deleteDir()
}

